# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Maintainer: Christoph Vigano <mail@cvigano.de>
# Maintainer: Mikhail Burakov <mikhail.burakov gmail com>

pkgname=st-custom
appname='st'
conflicts=(${appname})
provides=(${appname})
pkgver=0.7
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X. Patched for solarized colorscheme.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxext' 'libxft')
makedepends=('ncurses')
url="http://st.suckless.org"

source=(http://dl.suckless.org/st/$appname-$pkgver.tar.gz
        http://st.suckless.org/patches/st-no_bold_colors-20160727-308bfbf.diff
        http://st.suckless.org/patches/st-vertcenter-20160819-023225e.diff
        st-gruvbox-light.own.diff
        st-font.own.diff)


sha256sums=('f7870d906ccc988926eef2cc98950a99cc78725b685e934c422c03c1234e6000'
            '2e8cdbeaaa79ed067ffcfdcf4c5f09fb5c8c984906cde97226d4dd219dda39dc'
            'f6f8c5b61bbae220edd8019f52962e06dd01a25478e869e9fbc8d79f5a928730'
            'e3290f97d95b34c4cd20e0a3a9e3b7f78fa2c5eaf530baab9278f229975a4821'
            'a64e8c22b8f79c85523c4b1792f593de2ec712d477be82488401771cee497acd')

build() {
  cd "$srcdir/$appname-$pkgver"
  patch -i "$srcdir/st-no_bold_colors-20160727-308bfbf.diff"
  patch -i "$srcdir/st-vertcenter-20160819-023225e.diff"
  patch -i "$srcdir/st-gruvbox-light.own.diff"
  patch -i "$srcdir/st-font.own.diff"

  cp config.def.h config.h

  sed -i 's/Liberation Mono:pixelsize=12:antialias=true:autohint=true/Inconsolata:pixelsize=14:antialias=true:autohint=true/' config.h
  sed -i 's/chscale = 1.0/chscale = 1.2/' config.h
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$appname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install

  # Avoid conflict with ncurses package
  rm "$pkgdir/usr/share/terminfo/s/st"
  rm "$pkgdir/usr/share/terminfo/s/st-256color"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$appname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$appname/README"
}

# ad
